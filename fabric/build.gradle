import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'idea'
    id "net.darkhax.curseforgegradle" version "1.0.7"
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_name}"
version = "${rootProject.version}+${minecraft_version}-fabric"


loom {
    mixin.defaultRefmapName = "medievalorigins.refmap.json"

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}
repositories {
    maven {
        name = "Greenhouse"
        url = 'https://maven.greenhouseteam.dev/releases/'
    }
    maven {
        name = "Jamie's White Shirt"
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven { url = "https://maven.nucleoid.xyz/" }
    maven { url = 'https://maven.blamejared.com' }
    maven { url = "https://maven.willbl.dev/releases" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = 'https://maven.merchantpug.net/releases/' }
    maven { url = 'https://maven.ladysnake.org/releases' }
    maven {
        url = 'https://maven.cafeteria.dev'
        content {
            includeGroup 'net.adriantodt.fabricmc'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.bai.lol" }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://libs.azuredoom.com:4443/mods' }
    maven { url "https://maven.willbl.dev/releases" }
    maven { url "https://cursemaven.com" }
    maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }
    maven { url = "https://jm.gserv.me/repository/maven-public/" }

    mavenCentral()
    mavenLocal()
}

dependencies {
    //Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    //Fabric
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    //ModMenu
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
    //Origins
    modImplementation("com.github.apace100:calio:${calio_fabric_version}")
    modImplementation("com.github.apace100:apoli:${apoli_fabric_version}")
    modImplementation "com.github.apace100:origins-fabric:${origins_fabric_version}"
    //Apugli
    include(modImplementation("net.merchantpug:Apugli:${apugli_version}+${minecraft_version}-fabric"))
    // MixinExtras
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}"))
    //JSR305
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    //Service
    compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    // Water Walking Fix
    include(modImplementation("com.github.MerchantPug.water-walking-fix:WaterWalkingFix-Fabric:${wwfix_fabric_version}"))
    //Relying on the Common Project for sure
    implementation project(path: ":Common", configuration: "namedElements")

    // break - memememe


    modImplementation include("com.github.apace100:autotag-api:${autotag_version}")

    modImplementation include("maven.modrinth:projectile-damage-attribute:${project.projectile_damage}-fabric")



    modImplementation include("com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}")

    modApi("com.github.Virtuoel:Pehkui:${pehkui_version}", {
        exclude group: "net.fabricmc.fabric-api"
    })
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Icarus + Deps
    modImplementation ("dev.emi:trinkets:${project.trinkets_version}")
    modImplementation("maven.modrinth:icarus:${project.icarus_version}+Fabric")
    modLocalRuntime("com.teamresourceful.resourcefulconfig:resourcefulconfig-fabric-1.20.1:${project.resourceful_version}")
    modLocalRuntime ("mysticdrew:common-networking-fabric:${project.cmnw_version}")

    // RPG Series
    modLocalRuntime("maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}")
    modImplementation("maven.modrinth:spell-power:${project.spell_power_version}-fabric")
    modImplementation("maven.modrinth:spell-engine:${project.spell_engine_version}-fabric")
    modLocalRuntime("maven.modrinth:runes:${project.runes_version}-fabric")
    modImplementation("maven.modrinth:better-combat:${project.bettercombat_version}-fabric")
    modLocalRuntime("maven.modrinth:structure-pool-api:${project.structure_pool_api_version}-fabric")

    modLocalRuntime("maven.modrinth:wizards:${project.wizards_version}-fabric")
    modLocalRuntime("maven.modrinth:paladins-and-priests:${project.paladins_version}-fabric")
    modLocalRuntime("maven.modrinth:archers:${project.archers_version}-fabric")
    modLocalRuntime("maven.modrinth:jewelry:${project.jewelry_version}-fabric")
    modLocalRuntime("maven.modrinth:rpgdifficulty:${project.rpg_version}")

    modImplementation "curse.maven:rpgmana-1021902:5447010"

    modLocalRuntime("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim_version}")
    implementation("com.github.ZsoltMolnarrr:TinyConfig:${project.tiny_config_version}")
    modLocalRuntime("mod.azure.azurelibarmor:azurelibarmor-fabric-${project.minecraft_version}:${project.azure_lib_version}")
    modCompileOnly ("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}")
    modRuntimeOnly ("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")
    modRuntimeOnly ("curse.maven:jade-324717:${project.jade_id}")

    modImplementation "io.github.ladysnake:PlayerAbilityLib:${pal_version}"

}

processResources {
    from project(":Common").sourceSets.main.resources
    exclude "medievalorigins.common.accesswidener"

    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processTestResources {
    outputs.upToDateWhen { false }
    from project(":Common").sourceSets.test.resources
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "greenhouse"
            url = "https://maven.greenhouseteam.dev/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
        mavenLocal()
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(cf_project_id, remapJar)
    mainFile.displayName = "Fabric v${rootProject.version}+${minecraft_version}"
    mainFile.releaseType = "release"
    mainFile.changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = "markdown"
    fabric_mod_loaders.split(", ").each {
        mainFile.addModLoader(it)
    }
    fabric_game_versions.split(", ").each {
        mainFile.addGameVersion(it)
    }
    java_versions.split(", ").each {
        mainFile.addJavaVersion(it)
    }
    fabric_required_deps.split(", ").each {
        mainFile.addRequirement(it)
    }
    common_embedded_deps.split(", ").each {
        mainFile.addEmbedded(it)
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = mr_project_id
    versionNumber = "${version}"
    versionType = "release"
    versionName = "Fabric/Quilt v${rootProject.version}+${minecraft_version}"
    changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    uploadFile = remapJar
    gameVersions = fabric_game_versions.split(", ").toList()
    loaders = fabric_mod_loaders.split(", ").collect { return it.toLowerCase(Locale.ROOT) }
    dependencies {
        fabric_required_deps.split(", ").each {
            required.project it
        }

        common_embedded_deps.split(", ").each {
            embedded.project it
        }
    }
}